apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'application'

group = "com.geneticrypt"
version = "SNAPSHOT"
mainClassName = "com.nomachetejuggling.geneticrypt.cli.Main"

repositories {
    mavenLocal()
    mavenCentral()
}

idea.module.ext {
    gradleCacheVariable = 'GRADLE_CACHE'
    downloadJavadoc = true
}

idea.project {
    jdkName = '1.6'
    wildcards = ['!?*.java', '!?*.groovy', '!?*.scala']
}

idea.project.ipr.withXml { provider ->
    def node = provider.asNode()
    // Use git
    def vcsConfig = node.component.find { it.'@name' == 'VcsDirectoryMappings' }
    vcsConfig.mapping[0].'@vcs' = 'Git'

    // Set gradle home
    def gradleSettings = node.appendNode('component', [name: 'GradleSettings'])
    gradleSettings.appendNode('option', [name: 'SDK_HOME', value: gradle.gradleHomeDir.absolutePath])
}

sourceCompatibility = 1.6
compileJava.enabled = false
sourceSets.main.groovy.srcDirs = ["src/main/java", "src/main/groovy"]

dependencies {
    groovy group: 'org.codehaus.groovy', name: 'groovy', version: '1.7.10'

    testCompile 'junit:junit:4.10'
    testCompile 'org.hamcrest:hamcrest-core:1.3.RC2'
    testCompile 'org.hamcrest:hamcrest-library:1.3.RC2'

    compile 'commons-cli:commons-cli:1.2'

    compile 'com.nomachetejuggling.geneticrypt:geneticrypt-core:SNAPSHOT'
}


task "create-dirs" << {
    sourceSets*.java.srcDirs*.each { it.mkdirs() }
    sourceSets*.resources.srcDirs*.each { it.mkdirs() }
}